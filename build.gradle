buildscript {
    ext {
        repos = {
            gradlePluginPortal()
            mavenLocal()
            mavenCentral()
        }
        lombokVersion = '1.18.12'
        springBootVersion = '2.3.0.RELEASE'
        jacksonDatabindVersion = '2.11.0'
        swaggerVersion = '2.9.2'
        pitestVersion = '1.4.6'
        contractPublisherVersion = '1.0.9'

        repositories(repos)
    }
}


plugins {
    id 'groovy'
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'com.hltech.judged.contracts.publisher' version "$contractPublisherVersion"
    id 'info.solidsoft.pitest' version "$pitestVersion"
}

apply from: 'gradle/test.gradle'
apply from: 'gradle/version.gradle'

group 'com.trzewik'
version gitVersion()

sourceCompatibility = 1.8

repositories(repos)

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonDatabindVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

bootJar {
    mainClassName = 'com.trzewik.information.producer.App'
    manifest {
        attributes 'Start-Class': 'com.trzewik.information.producer.App'
    }
    launchScript()
}

bootRun {
    main = 'com.trzewik.information.producer.App'
}

pitest {
    useClasspathFile = true
    threads = 2
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
